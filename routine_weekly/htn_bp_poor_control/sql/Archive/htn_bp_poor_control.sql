set nocount on;

SELECT  TOP 10000000
	pev.PAT_ID
	,pev.DEPARTMENT_ID
	,dep.DEPARTMENT_NAME LAST_VISIT_DEPT
	,pev.CONTACT_DATE LAST_OFFICE_VISIT
	,SUBSTRING(dep.DEPT_ABBREVIATION, 3, 2) 'STATE'
	,CASE
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'MK' THEN 'MILWAUKEE'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'KN' THEN 'KENOSHA'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'GB' THEN 'GREEN BAY'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'WS' THEN 'WAUSAU'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'AP' THEN 'APPLETON'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'EC' THEN 'EAU CLAIRE'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'LC' THEN 'LACROSSE'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'MD' THEN 'MADISON'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'BL' THEN 'BELOIT'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'BI' THEN 'BILLING'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'SL' THEN 'ST LOUIS'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'DN' THEN 'DENVER'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'AS' THEN 'AUSTIN'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'KC' THEN 'KANSAS CITY'
		ELSE SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2)
	END AS CITY
	,CASE
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'MN' THEN 'MAIN LOCATION'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'DR' THEN 'D&R'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'KE' THEN 'KEENEN'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'UC' THEN 'UNIVERSITY OF COLORADO'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'ON' THEN 'AUSTIN MAIN'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'TW' THEN 'AUSTIN OTHER'
		ELSE 'ERROR'
	END AS 'SITE'
	,CASE
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'MD' THEN 'MEDICAL'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'DT' THEN 'DENTAL'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'CM' THEN 'CASE MANAGEMENT'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'RX' THEN 'PHARMACY'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'AD' THEN 'BEHAVIORAL'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'PY' THEN 'BEHAVIORAL'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'BH' THEN 'BEHAVIORAL'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'MH' THEN 'BEHAVIORAL'
		ELSE 'ERROR'
	END AS 'LOS'

INTO #Attribution1

FROM 
	Clarity.dbo.PAT_ENC_VIEW pev
	INNER JOIN Clarity.dbo.CLARITY_DEP_VIEW dep ON pev.DEPARTMENT_ID = dep.DEPARTMENT_ID

WHERE
	pev.CONTACT_DATE > DATEADD(MONTH, -12, GETDATE())
	AND pev.APPT_STATUS_C IN (2, 6)

;

SELECT  TOP 10000000
	a1.PAT_ID
	,a1.LAST_OFFICE_VISIT
	,a1.LOS
	,a1.CITY
	,a1.STATE
	,ROW_NUMBER() OVER (PARTITION BY a1.PAT_ID ORDER BY a1.LAST_OFFICE_VISIT DESC) AS ROW_NUM_DESC

INTO #Attribution2

FROM 
	#Attribution1 a1

WHERE
	a1.LOS = 'MEDICAL'
	AND a1.PAT_ID IN 
			(SELECT DISTINCT
				pev.PAT_ID

			FROM 
				Clarity.dbo.PATIENT_VIEW p
				INNER JOIN Clarity.dbo.PATIENT_4 p4 ON p.PAT_ID = p4.PAT_ID
				INNER JOIN Clarity.dbo.PAT_ENC_VIEW pev ON p.PAT_ID = pev.PAT_ID
				INNER JOIN Clarity.dbo.CLARITY_DEP_VIEW dep ON dep.DEPARTMENT_ID = pev.DEPARTMENT_ID
				INNER JOIN Clarity.dbo.PROBLEM_LIST_VIEW plv ON pev.PAT_ID = plv.PAT_ID
				INNER JOIN Clarity.dbo.CLARITY_EDG edg ON plv.DX_ID = edg.DX_ID
				INNER JOIN Clarity.dbo.EDG_CURRENT_ICD10 icd10 ON edg.DX_ID = icd10.DX_ID
				INNER JOIN Clarity.dbo.CLARITY_SER_VIEW ser ON p.CUR_PCP_PROV_ID = ser.PROV_ID

			WHERE
				ser.SERV_AREA_ID = 64
				AND ser.PROVIDER_TYPE_C IN ('1', '9', '6', '113') -- Physicians and NPs, PAs
				AND pev.CONTACT_DATE > DATEADD (MM,-12, CURRENT_TIMESTAMP) --Visit in past year
				AND pev.APPT_STATUS_C IN (2, 6) --Visit was completed
				AND pev.LOS_PRIME_PROC_ID IN (7945, 7946, 7947, 7948, 7949, 7951, 7952, 7953, 7954, 7970, 7971, 7972, 
											7973, 7974, 8047, 8048, 8049, 8050, 8051, 8052, 8053, 8054, 8055, 8056) -- Office Visits
				AND SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'MD'-- Visit was in a medical department
				AND icd10.CODE IN ('B20', 'Z21', 'B97.35') --HIV and Asymptomatic HIV
				AND plv.RESOLVED_DATE IS NULL --Active Dx
				AND plv.PROBLEM_STATUS_C = 1 --Active Dx
				AND p4.PAT_LIVING_STAT_C = 1
			)

;
SELECT  TOP 10000000
	id.IDENTITY_ID
	,id.PAT_ID
	,p.PAT_NAME
	,a2.STATE
	,a2.CITY
	,dwav.BP_SYS_LAST_DT
	,dwav.BP_SYS_LAST
	,dwav.BP_DIA_LAST
	,ser.EXTERNAL_NAME
	,CASE
		WHEN cp.PAT_ID IS NOT NULL THEN 'YES'
		ELSE 'NO'
	END AS 'IN CLINICAL PARM COHORT'
	,CASE
		WHEN rd.PAT_ID IS NOT NULL THEN 'YES'
		ELSE 'NO'
	END AS 'IN DIETITIAN CARE'

INTO #a

FROM Clarity.dbo.PATIENT_VIEW p
	INNER JOIN #Attribution2 a2 ON p.PAT_ID = a2.PAT_ID
	INNER JOIN Clarity.dbo.IDENTITY_ID_VIEW id ON p.PAT_ID = id.PAT_ID
	INNER JOIN Clarity.dbo.CLARITY_SER_VIEW ser ON p.CUR_PCP_PROV_ID = ser.PROV_ID
	INNER JOIN Clarity.dbo.DM_WLL_ALL_VIEW dwav ON p.PAT_ID = dwav.PAT_ID
	LEFT JOIN 
		(SELECT
			flag.PATIENT_ID AS PAT_ID
			,MAX(flag.ACTIVE_C) ACTIVE

		FROM 
			Clarity.dbo.PATIENT_FYI_FLAGS_VIEW flag 
		WHERE
			flag.PAT_FLAG_TYPE_C = '640025'  -- HTN- Dietitian care
			AND flag.ACTIVE_C = 1

		GROUP BY 
			flag.PATIENT_ID 

		) rd ON rd.PAT_ID = p.PAT_ID
	LEFT JOIN 
		(SELECT
			flag.PATIENT_ID AS PAT_ID
			,MAX(flag.ACTIVE_C) ACTIVE

		FROM 
			Clarity.dbo.PATIENT_FYI_FLAGS_VIEW flag 
			INNER JOIN Clarity.dbo.ZC_BPA_TRIGGER_FYI f ON flag.PAT_FLAG_TYPE_C = f.BPA_TRIGGER_FYI_C

		WHERE
			f.name = 'SA64 Pharmacist - HTN'
			AND flag.ACTIVE_C = 1
		GROUP BY 
			flag.PATIENT_ID 
		) cp ON cp.PAT_ID = p.PAT_ID

WHERE
	a2.ROW_NUM_DESC = 1
	AND dwav.HAS_HYPERTENSION_YN = 'Y'

;

SELECT  TOP 10000000
	a.IDENTITY_ID
	,a.PAT_ID
	,a.PAT_NAME
	,a.STATE
	,a.CITY
	,a.EXTERNAL_NAME
	,a.[IN CLINICAL PARM COHORT]
	,a.[IN DIETITIAN CARE]
	,a.BP_SYS_LAST_DT LAST_BP
	,a.BP_SYS_LAST LATEST_SYSTOLIC
	, a.BP_DIA_LAST AS LATEST_DIASTOLIC
INTO #b

FROM #a a

;

SELECT TOP 100000000
	b.IDENTITY_ID
	,b.PAT_ID
	,b.PAT_NAME
	,b.STATE
	,b.CITY
	,b.EXTERNAL_NAME PROV_NAME
	,b.LAST_BP
	,b.LATEST_SYSTOLIC
	,b.LATEST_DIASTOLIC
	,b.[IN CLINICAL PARM COHORT]
	,CASE
		WHEN zeg.NAME IS NULL THEN 'Unknown'
		WHEN zeg.NAME = '' THEN 'Unknown'
		WHEN zeg.NAME = 'Not Collected/Unknown' THEN 'Unknown'
		WHEN zeg.NAME = 'Patient Refused' THEN 'Unknown'
		ELSE zeg.NAME
	END AS ETHNICITY
	,CASE
		WHEN b.LATEST_SYSTOLIC < 140 AND b.LATEST_DIASTOLIC < 90 THEN
			1
		ELSE
			0
	END AS MET_YN
	,CASE
		WHEN b.LATEST_SYSTOLIC < 140 AND b.LATEST_DIASTOLIC < 90 THEN
			'MET'
		ELSE
			'NOTMET'
	END AS OUTCOME
	,b.[IN DIETITIAN CARE]
	,'CLICK HERE FOR PATIENT DETAIL' 'CLICK HERE FOR PATIENT DETAIL' 
	,zpr.NAME 'RACE'
	,svis.[Next Any Appt]
	,svis.[Next Appt Prov]
	,spvis.[Next PCP Appt]
	,spvis.[Next PCP Appt Prov]
	,COALESCE(ep.[Active BH], 'N') 'Active BH'
	,COALESCE(ep.[Active Dental], 'Y') 'Active Dental'
	   
FROM 
	#b b
	LEFT JOIN Clarity.dbo.PATIENT_RACE pr ON pr.PAT_ID = b.PAT_ID
							AND pr.LINE = 1
	LEFT JOIN Clarity.dbo.ZC_PATIENT_RACE zpr ON zpr.PATIENT_RACE_C = pr.PATIENT_RACE_C
	INNER JOIN Clarity.dbo.PATIENT_VIEW p ON p.PAT_ID = b.PAT_ID
	LEFT JOIN Clarity.dbo.ZC_ETHNIC_GROUP zeg ON zeg.ETHNIC_GROUP_C = p.ETHNIC_GROUP_C
	LEFT JOIN
		(SELECT  TOP 1000000
		 	pev.PAT_ID
			,pev.CONTACT_DATE 'Next Any Appt'
			,ser.PROV_NAME 'Next Appt Prov'
			,ROW_NUMBER() OVER (PARTITION BY pev.PAT_ID ORDER BY pev.CONTACT_DATE ASC) AS ROW_NUM_ASC

		 FROM 
		 	Clarity.dbo.PAT_ENC_VIEW pev
			INNER JOIN Clarity.dbo.CLARITY_SER_VIEW ser ON pev.VISIT_PROV_ID = ser.PROV_ID

		WHERE
			pev.APPT_STATUS_C = 1 --Scheduled

		) svis ON svis.PAT_ID = b.PAT_ID
				AND svis.ROW_NUM_ASC = 1 -- First scheduled
	LEFT JOIN
		(SELECT  TOP 1000000
		 	pev.PAT_ID
			,pev.CONTACT_DATE 'Next PCP Appt'
			,ser.PROV_NAME 'Next PCP Appt Prov'
			,ROW_NUMBER() OVER (PARTITION BY pev.PAT_ID ORDER BY pev.CONTACT_DATE ASC) AS ROW_NUM_ASC

		 FROM 
		 	Clarity.dbo.PAT_ENC_VIEW pev
			INNER JOIN Clarity.dbo.CLARITY_SER_VIEW ser ON pev.VISIT_PROV_ID = ser.PROV_ID

		WHERE
			pev.APPT_STATUS_C = 1 --Scheduled
			AND ser.PROVIDER_TYPE_C IN ('1', '6', '9', '113') -- Physicians, PAs and NPs

		) spvis ON spvis.PAT_ID = b.PAT_ID
				AND spvis.ROW_NUM_ASC = 1 -- First scheduled
	LEFT JOIN
		(SELECT TOP 100000000
			ev.PAT_LINK_ID PAT_ID
			,MAX(CASE
				WHEN ev.SUM_BLK_TYPE_ID = 221 THEN 'Y'
				ELSE 'N'
			END) AS 'Active BH'
			,MAX(CASE
				WHEN ev.SUM_BLK_TYPE_ID = 45 THEN 'Y'
				ELSE 'N'
			END) AS 'Active Dental'

		FROM 
			Clarity.dbo.EPISODE_VIEW ev
			INNER JOIN Clarity.dbo.CLARITY_EMP_VIEW emp ON ev.L_UPDATE_USER_ID = emp.USER_ID

		WHERE
			ev.SUM_BLK_TYPE_ID IN (45, 221) -- Dental and BH
			AND ev.STATUS_C = 1

		GROUP BY 
			ev.PAT_LINK_ID
			) ep ON ep.PAT_ID = b.PAT_ID

WHERE
	b.PAT_ID NOT IN
		(SELECT DISTINCT TOP 100000000
		 	plv.PAT_ID 

		 FROM 
		 	Clarity.dbo.PROBLEM_LIST_VIEW plv
			INNER JOIN Clarity.dbo.CLARITY_EDG edg ON edg.DX_ID = plv.DX_ID
			INNER JOIN Clarity.dbo.GROUPER_COMPILED_REC_LIST gc ON gc.GROUPER_RECORDS_NUMERIC_ID = edg.DX_ID
			INNER JOIN Clarity.dbo.GROUPER_ITEMS gi ON gc.BASE_GROUPER_ID = gi.GROUPER_ID

		WHERE
			gi.GROUPER_ID IN ('107089' /*Diabetic Nephropathy*/,'5200000134' /*Dialysis*/)
			AND plv.RESOLVED_DATE IS NULL
			AND plv.PROBLEM_STATUS_C = 1

		)
;

DROP TABLE #a
DROP TABLE #b
DROP TABLE #Attribution1
DROP TABLE #Attribution2