SET NOCOUNT ON;
SET ANSI_WARNINGS OFF;

IF OBJECT_ID('tempdb..#a') IS NOT NULL DROP TABLE #a;
SELECT id.IDENTITY_ID,
       p.PAT_NAME,
       orv.ORD_NUM_VALUE,
       orv.RESULT_DATE,
       ROW_NUMBER() OVER (PARTITION BY id.IDENTITY_ID ORDER BY orv.RESULT_DATE DESC) AS ROW_NUM_DESC
INTO #a
FROM Clarity.dbo.PATIENT_VIEW p
    INNER JOIN Clarity.dbo.ORDER_PROC_VIEW opv ON p.PAT_ID = opv.PAT_ID
    INNER JOIN Clarity.dbo.ORDER_RESULTS_VIEW orv ON opv.ORDER_PROC_ID = orv.ORDER_PROC_ID
    INNER JOIN Clarity.dbo.IDENTITY_ID_VIEW id ON id.PAT_ID = p.PAT_ID
    INNER JOIN Clarity.dbo.PATIENT_FYI_FLAGS_VIEW flag ON p.PAT_ID = flag.PATIENT_ID
WHERE flag.PAT_FLAG_TYPE_C = '640013'
      --AND flag.ACCT_NOTE_INSTANT < '10/1/2019'  --For eval only, not for operations. Uncomment this line when pulling quality numbers.
      AND flag.ACTIVE_C = 1
      AND opv.ORDER_TYPE_C = 7
      AND orv.ORD_NUM_VALUE <> 9999999
      AND orv.COMPONENT_ID IN ( 12803, 15513, 2025, 22852, 4510, 65551,
                                65665, 81, 9586 )
      AND id.IDENTITY_TYPE_ID = '64'
      AND orv.RESULT_DATE > DATEADD(MONTH, -24, GETDATE());

IF OBJECT_ID('tempdb..#b') IS NOT NULL DROP TABLE #b;
SELECT a.IDENTITY_ID,
       a.PAT_NAME,
       MAX(CASE WHEN a.ROW_NUM_DESC = 1 THEN a.ORD_NUM_VALUE END) AS NEWEST,
       MAX(CASE WHEN a.ROW_NUM_DESC = 1 THEN a.RESULT_DATE END) AS NEWEST_DATE,
       MAX(CASE WHEN a.ROW_NUM_DESC = 2 THEN a.ORD_NUM_VALUE END) AS SECOND_LAB,
       MAX(CASE WHEN a.ROW_NUM_DESC = 2 THEN a.RESULT_DATE END) AS SECOND_DATE
INTO #b
FROM #a a
GROUP BY a.IDENTITY_ID,
         a.PAT_NAME;

IF OBJECT_ID('tempdb..#c') IS NOT NULL DROP TABLE #c;
SELECT b.IDENTITY_ID, b.PAT_NAME, b.NEWEST, b.SECOND_LAB INTO #c FROM #b b;

SELECT c.IDENTITY_ID MRN,
       c.PAT_NAME PATIENT,
       c.NEWEST,
       c.SECOND_LAB,
       CASE WHEN c.SECOND_LAB < 7
                THEN 'CONTROLLED'
       WHEN c.SECOND_LAB < 9
           THEN 'MODERATELY CONTROLLED'
       ELSE 'UNCONTROLLED'
       END AS PREVIOUS_A1c_STATUS,
       CASE WHEN c.NEWEST < 7
                THEN 'CONTROLLED'
       WHEN c.NEWEST < 9
           THEN 'MODERATELY CONTROLLED'
       ELSE 'UNCONTROLLED'
       END AS A1c_STATUS,
       CONVERT(NVARCHAR(30), GETDATE(), 101) AS TODAY
FROM #c c;