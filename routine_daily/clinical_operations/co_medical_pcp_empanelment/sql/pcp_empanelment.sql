SET NOCOUNT ON;
SET ANSI_WARNINGS OFF;

SELECT 
	pev.PAT_ID,
	pev.CONTACT_DATE LAST_OFFICE_VISIT,
	SUBSTRING(dep.DEPT_ABBREVIATION, 3, 2) 'STATE',
	CASE WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'MK' THEN 'MILWAUKEE'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'KN' THEN 'KENOSHA'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'GB' THEN 'GREEN BAY'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'WS' THEN 'WAUSAU'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'AP' THEN 'APPLETON'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'EC' THEN 'EAU CLAIRE'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'LC' THEN 'LACROSSE'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'MD' THEN 'MADISON'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'BL' THEN 'BELOIT'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'BI' THEN 'BILLING'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'SL' THEN 'ST LOUIS'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'DN' THEN 'DENVER'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'AS' THEN 'AUSTIN'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'KC' THEN 'KANSAS CITY'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 5, 2) = 'CG' THEN 'CHICAGO'
		ELSE 'ERROR'
	END AS CITY,
	CASE WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'MN' THEN 'MAIN LOCATION'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'DR' THEN 'D&R'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'KE' THEN 'KEENEN'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'UC' THEN 'UNIVERSITY OF COLORADO'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'ON' THEN 'AUSTIN MAIN'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 7, 2) = 'TW' THEN 'AUSTIN OTHER'
		ELSE 'ERROR'
	END AS 'SITE',
	CASE WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'MD' THEN 'MEDICAL'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'DT' THEN 'DENTAL'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'CM' THEN 'CASE MANAGEMENT'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'RX' THEN 'PHARMACY'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'AD' THEN 'BEHAVIORAL'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'PY' THEN 'BEHAVIORAL'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'BH' THEN 'BEHAVIORAL'
		WHEN SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'MH' THEN 'BEHAVIORAL'
		ELSE 'ERROR'
	END AS 'LOS'

INTO #Attribution1

FROM 
	CLARITY.dbo.PAT_ENC_VIEW pev
    INNER JOIN CLARITY.dbo.CLARITY_DEP_VIEW dep ON dep.DEPARTMENT_ID = pev.DEPARTMENT_ID

WHERE 
	pev.CONTACT_DATE > DATEADD(MONTH, -60, GETDATE()) --Longer lookback for pts not seen in a long time
    AND pev.APPT_STATUS_C IN ( 2, 6 )
;

SELECT 
	a1.PAT_ID,
	a1.STATE,
	a1.CITY,
	a1.SITE,
	a1.LOS,
	ROW_NUMBER() OVER (PARTITION BY a1.PAT_ID ORDER BY a1.LAST_OFFICE_VISIT DESC) AS ROW_NUM_DESC

INTO #Attribution2

FROM 
	#Attribution1 a1

WHERE a1.LOS = 'MEDICAL'
;

SELECT a2.PAT_ID, a2.LOS, a2.CITY, a2.STATE INTO #Attribution3 FROM #Attribution2 a2 WHERE a2.ROW_NUM_DESC = 1;

SELECT 
	id.IDENTITY_ID MRN,
	p.PAT_ID,
	p.PAT_NAME PATIENT,
	ser.EXTERNAL_NAME PCP,
	a3.CITY,
	a3.STATE,
	MAX(CASE WHEN f.PAT_FLAG_TYPE_C = '640005' THEN 'PrEP'
			WHEN f.PAT_FLAG_TYPE_C = '640008' THEN 'STI'
			WHEN f.PAT_FLAG_TYPE_C = '9800035' THEN 'PEP'
			WHEN f.PAT_FLAG_TYPE_C = '640017' THEN 'False Positive HIV'
			ELSE 'HIV'
	END) AS 'PATIENT TYPE'

INTO #a

FROM 
	CLARITY.dbo.PATIENT_VIEW p
    INNER JOIN #Attribution3 a3 ON a3.PAT_ID = p.PAT_ID
    INNER JOIN CLARITY.dbo.IDENTITY_ID_VIEW id ON p.PAT_ID = id.PAT_ID
    LEFT JOIN CLARITY.dbo.CLARITY_SER_VIEW ser ON p.CUR_PCP_PROV_ID = ser.PROV_ID
    LEFT JOIN CLARITY.dbo.PATIENT_FYI_FLAGS_VIEW f ON p.PAT_ID = f.PATIENT_ID
                                                      AND f.PAT_FLAG_TYPE_C IN ( '640005', '640008', '9800035', '640007' )
                                                      AND f.ACTIVE_C = 1

WHERE 
	p.CUR_PCP_PROV_ID IS NOT NULL
    AND ser.SERV_AREA_ID = 64

GROUP BY 
	id.IDENTITY_ID,
	p.PAT_NAME,
	ser.EXTERNAL_NAME,
	a3.CITY,
	a3.STATE,
	p.PAT_ID
		 
;

SELECT 
	a.PAT_ID,
	pev.CONTACT_DATE 'LAST VISIT',
	ROW_NUMBER() OVER (PARTITION BY a.PAT_ID ORDER BY pev.CONTACT_DATE DESC) AS ROW_NUM_DESC

INTO #last

FROM 
	#a a
    INNER JOIN CLARITY.dbo.PAT_ENC_VIEW pev ON pev.PAT_ID = a.PAT_ID
    INNER JOIN CLARITY.dbo.CLARITY_DEP_VIEW dep ON dep.DEPARTMENT_ID = pev.DEPARTMENT_ID
    LEFT JOIN CLARITY.dbo.CLARITY_SER_VIEW ser ON pev.VISIT_PROV_ID = ser.PROV_ID

WHERE SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'MD'
      AND ser.SERV_AREA_ID = 64
      AND ser.PROVIDER_TYPE_C IN ( '1', '9', '6', '113' ) -- Physicians and NPs, PAs
      AND pev.APPT_STATUS_C IN ( 2, 6 )
;

SELECT 
	a.PAT_ID,
	pev.CONTACT_DATE 'NEXT VISIT',
	ser.EXTERNAL_NAME 'Next Visit Provider',
	ROW_NUMBER() OVER (PARTITION BY a.PAT_ID ORDER BY pev.CONTACT_DATE ASC) AS ROW_NUM_ASC

INTO #next

FROM 
	#a a
    INNER JOIN CLARITY.dbo.PAT_ENC_VIEW pev ON pev.PAT_ID = a.PAT_ID
                                               AND pev.APPT_STATUS_C = 1
    INNER JOIN CLARITY.dbo.CLARITY_DEP_VIEW dep ON dep.DEPARTMENT_ID = pev.DEPARTMENT_ID
                                                   AND SUBSTRING(dep.DEPT_ABBREVIATION, 9, 2) = 'MD'
    INNER JOIN CLARITY.dbo.CLARITY_SER_VIEW ser ON pev.VISIT_PROV_ID = ser.PROV_ID
                                                   AND ser.SERV_AREA_ID = 64
                                                   AND ser.PROVIDER_TYPE_C IN ( '1', '9', '6', '113' ) -- Physicians and NPs, PAs
;

SELECT 
	a.PAT_ID,
	pev.CONTACT_DATE 'NEXT VISIT',
	ser.EXTERNAL_NAME 'Next Visit Provider',
	ROW_NUMBER() OVER (PARTITION BY a.PAT_ID ORDER BY pev.CONTACT_DATE ASC) AS ROW_NUM_ASC

INTO #next_any

FROM 
	#a a
    INNER JOIN CLARITY.dbo.PAT_ENC_VIEW pev ON pev.PAT_ID = a.PAT_ID
                                               AND pev.APPT_STATUS_C = 1
    INNER JOIN CLARITY.dbo.CLARITY_DEP_VIEW dep ON dep.DEPARTMENT_ID = pev.DEPARTMENT_ID
    INNER JOIN CLARITY.dbo.CLARITY_SER_VIEW ser ON pev.VISIT_PROV_ID = ser.PROV_ID
                                                AND ser.SERV_AREA_ID = 64
;

SELECT 
	a.MRN,
	a.PAT_ID,
	a.PATIENT,
	a.PCP,
	a.CITY,
	a.STATE,
	a.[PATIENT TYPE],
	n.[Next Visit Provider],
	CAST(l.[LAST VISIT] AS DATE) AS 'LAST VISIT',
	CAST(n.[NEXT VISIT] AS DATE) AS 'NEXT VISIT',
	DATEDIFF(mm, l.[LAST VISIT], GETDATE()) 'MONTHS SINCE SEEN',
	na.[Next Visit Provider] 'Next Any Visit Provider',
	CAST(na.[NEXT VISIT] AS DATE) AS 'Next Any Visit'

FROM 
	#a a
    INNER JOIN #last l ON l.PAT_ID = a.PAT_ID
    LEFT JOIN #next n ON n.PAT_ID = a.PAT_ID
                         AND n.ROW_NUM_ASC = 1
    LEFT JOIN #next_any na ON na.PAT_ID = a.PAT_ID
                          AND na.ROW_NUM_ASC = 1

WHERE 
	l.ROW_NUM_DESC = 1
;

DROP TABLE #Attribution1;
DROP TABLE #Attribution2;
DROP TABLE #Attribution3;
DROP TABLE #a;
DROP TABLE #last;
DROP TABLE #next;
DROP TABLE #next_any;